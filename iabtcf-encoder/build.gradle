apply plugin: "com.android.library"
apply plugin: "maven-publish"


android {
    compileSdk 33

    namespace 'com.smaato.iabtcf'

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 33
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

}

dependencies {
    implementation project(path: ':iabtcf-decoder')
    testImplementation 'junit:junit:4.12'
    testImplementation 'commons-io:commons-io:2.4'
    testImplementation 'org.hamcrest:hamcrest-junit:2.0.0.0'
    testImplementation project(path: ':iabtcf-decoder')
}

publishing.publications {
    release(MavenPublication) {
        groupId project.group
        artifactId project.name
        version project.version

        project.afterEvaluate {
            artifact project.tasks.named("bundleReleaseAar").get()
        }

        pom.withXml {
            def dependencies = asNode().appendNode('dependencies')
            project.configurations.api.dependencies.withType(org.gradle.api.internal.artifacts.dependencies.AbstractModuleDependency).each {
                def dependency = dependencies.appendNode('dependency')
                dependency.appendNode('groupId', it.group)
                dependency.appendNode('artifactId', it.name)
                dependency.appendNode('version', it.version)
                dependency.appendNode('scope', "compile")
            }
        }
    }
}
